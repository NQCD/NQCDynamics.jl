# [Semiclassical EBK quantisation](@id ebk-sampling)

In surface science, it is often of interest to investigate how collisions with surfaces
can perturb the quantum states of molecules.
In particular, for diatomic molecules, the rotational and vibrational quantum numbers
can undergo significant changes when the molecule impacts the surface.

Einstein-Brillouinn-Keller (EBK) quantisation allows for a semiclassical investigation
into these phenomena by providing a link between the quantum numbers and classical
positions and velocities.
The quantisation procedure allows the user to generate a classical distribution
with a given set of quantum numbers, then perform semiclassical dynamics
and extract the quantum numbers at the end by reversing the procedure.

These three steps can be applied to give insight into the processes taking place
during surface scattering and allow us to attempt to predict the experimentally
observed change in the quantum numbers.

A detailed yet approachable description of the theory is given by [Larkoski2006](@cite)
so we shall not delve into the theory here.
Briefly, the procedure for a diatomic molecule involves an optimisation process to
find the bounds of an integral, then computing the integral to obtain the vibrational
quantum number.
The rotational quantum number comes directly from the classical angular momentum
of the molecule.

Configurations can be generated by randomly selecting bond lengths from the appropriate
probability distribution and selecting a matching radial velocity.

## Example

In this example we will create a quantised distribution suitable for use as initial
conditions for hydrogen scattering simulations.

The simulation can be set up in the usual way, by specifying the atoms along with the
model and the simulation cell.
```@example ebk
using NonadiabaticMolecularDynamics
using Unitful

atoms = Atoms([:H, :H])
model = DiatomicHarmonic()
cell = PeriodicCell(austrip.([5.883 -2.942 0; 0 5.095 0; 0 0 20] .* u"Å"))
sim = Simulation(atoms, model; cell=cell)
```

The distribution is generated using the
[`QuantisedDiatomic.generate_configurations`](@ref InitialConditions.QuantisedDiatomic.generate_configurations)
function.
We have to provide the desired vibrational `ν` and rotational `J` quantum numbers,
along with the number of samples and some other options as keyword arguments.
In addition to the rotational and vibrational energy we have applied a translational impulse
of 1 eV and positioned the molecule at a height of 10 bohr.
```@example ebk
using NonadiabaticMolecularDynamics.InitialConditions: QuantisedDiatomic

ν, J = 2, 0
nsamples = 150

configurations = QuantisedDiatomic.generate_configurations(sim, ν, J;
    samples=nsamples, translational_energy=1u"eV", height=10)
```

The output contains both the positions and velocities, these can be passed directly
to the [`InitialConditions.DynamicalDistribution`](@ref) for use with dynamics.
Here however, let's focus on the positions and visualise the distribution.

This collects the x and y coordinate for each atom:
```@example ebk
r = last.(configurations)
x = hcat([i[1,:] for i in r]...)
y = hcat([i[2,:] for i in r]...)
```

Now we can plot the distribution using [CairoMakie](https://github.com/JuliaPlots/Makie.jl)
```@example ebk
using CairoMakie

f = Figure()
ax = Axis(f[1,1], xlabel="x coordinate / bohr", ylabel="y coordinate / bohr")
ax.aspect = AxisAspect(1)
hidedecorations!(ax; ticks=false, ticklabels=false, label=false)

for i=1:nsamples
    lines!([x[1,i], x[2,i]], [y[1,i], y[2,i]], linewidth=5, color=:black)
end

colors = rand(nsamples)
scatter!(x[1,:], y[1,:]; markersize=20, color=colors)
scatter!(x[2,:], y[2,:]; markersize=20, color=colors)

f
```
Here we can see that the molecule is randomly distributed within the unit cell.
Since we have used a harmonic potential, this could have been produced without using the EBK
procedure, but this technique can use any arbitrary potential.
In the [hydrogen scattering example](@id example-h2scattering) we build on this example
and use the sample procedure to perform scattering simulations starting from this
distribution.
